!
!  OPENDSS USER'S MANUAL EXAMPLE CIRCUIT 2
!

Clear

new object=circuit.DSSLLibtestckt
~ basekv=115  1.00 0.0 60.0 3 20000 21000 4.0 3.0  !edit the voltage source

! define a linecode for the lines - unbalanced 336 MCM ACSR connection
new linecode.336matrix nphases=3   ! horizontal flat construction
~  rmatrix=(0.0868455 |  0.0298305 0.0887966 | 0.0288883 0.0298305  0.0868455) ! ohms per 1000 ft 
~  xmatrix=(0.2025449 |  0.0847210 0.1961452 | 0.0719161 0.0847210  0.2025449)
~  cmatrix=(2.74 | -0.70 2.96| -0.34 -0.71 2.74)  !nf per 1000 ft
~  Normamps = 400  Emergamps=600

! Substation transformer
new transformer.sub phases=3 windings=2 buses=(SourceBus subbus) conns='delta wye' kvs="115 12.47 " kvas="20000 20000" XHL=7

! define the lines  (Make sure they have unique names!)

! Feeder 1
new line.line1-1 subbus   F1-1 336matrix 15 
new line.line1-2 F1-1     F1-2 336matrix 15
new line.line1-3 F1-2     F1-3 336matrix 15

! Feeder 2
new line.line2-1 subbus   F2-1 336matrix 15 
new line.line2-2 F2-1     F2-2 336matrix 15
new line.line2-3 F2-2     F2-3 336matrix 15

! Feeder 3
new line.line3-1 subbus   F3-1 336matrix 15 
new line.line3-2 F3-1     F3-2 336matrix 15
new line.line3-3 F3-2     F3-3 336matrix 15

 

! Define 3 transformer of different connections on each feeder midpoint
new transformer.TR1 phases=3 windings=2 
~           buses=(F1-2 Genbus1) 
~           conns='wye wye' 
~           kvs="12.47 0.48" 
~           kvas="5000 5000" 
~           XHL=6              

new transformer.TR2 phases=3 windings=2 
~           buses=(F2-2 Genbus2) 
~           conns='delta wye' 
~           kvs="12.47 0.48" 
~           kvas="5000 5000" 
~           XHL=6              

new transformer.TR3 phases=3 windings=2 
~           buses=(F3-2 Genbus3) 
~           conns='delta delta' 
~           kvs="12.47 0.48" 
~           kvas="5000 5000" 
~           XHL=6   

! put some loads on the transformers 
new load.load1 bus1=Genbus1 phases=3 kv=0.48 kw=1000.0 pf=0.88 model=1 class=1  status=fixed
new load.load2 bus1=Genbus2 phases=3 kv=0.48 kw=1000.0 pf=0.88 model=1 class=1  status=fixed
new load.load3 bus1=Genbus3 phases=3 kv=0.48 kw=1000.0 pf=0.88 model=1 class=1  status=fixed conn=delta

           
! Define monitors at the secondary buses to pick up voltage magnitudes

New Monitor.TR1 element=Transformer.TR1 2  mode=32
New Monitor.TR2 element=Transformer.TR2 2  mode=32
New Monitor.TR3 element=Transformer.TR3 2  mode=32


! Define voltage bases so voltage reports come out in per unit
Set voltagebases="115 12.47 .48"
Calcv

! define all kinds of faults at one bus (will be moved later)
! in Monte Carlo Fault mode, only one will be chosen at a time
New Fault.F1   bus1=F1-1.1 phases=1 r=2
New Fault.F2   bus1=F1-1.2 phases=1 r=2
New Fault.F3   bus1=F1-1.3 phases=1 r=2
New Fault.FALL bus1=F1-1   phases=3 r=2
New Fault.F12  bus1=F1-1.1 bus2=F1-1.2 phases=1 r=2
New Fault.F23  bus1=F1-1.2 Bus2=F1-1.3 phases=1 r=2

Set loadmodel=a
Set Toler=0.001 Random=uniform
